#pragma kernel CSMain

struct Ray
{
    float3 origin, direction;
};

struct HitData
{
    float distance;
    float3 normal, color;
};

struct Step
{
    float distance;
    float3 color;
};

struct Shape
{
    float3 position, color, size;
    float radius;
    int operation, type;
    float blendStrength;
    int childCount;
};


RWTexture2D<float4> Result;

float4x4 CameraToWorld, CameraInverseProjection;

RWStructuredBuffer<Shape> Shapes;

static const uint MaxSteps = 100;
static const float MinDistance = 0.01f;
static const float MaxDistance = 100;
static const float infinity = 3.402823466e+38;

static const int OperationNone = 0;
static const int OperationCut = 1;
static const int OperationMask = 2;
static const int OperationBlend = 3;

static const int ShapeSphere = 0;
static const int ShapeBox = 1;
static const int ShapeOctahedron = 2;
static const int ShapePlane = 3;
static const int ShapeTorus = 4;

uint shapeCount;


Ray CreateRay(float3 origin, float3 direction)
{
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;

    return ray;
}

Ray CreateCameraRay(float2 uv)
{
    float3 origin = mul(CameraToWorld, float4(0, 0, 0, 1)).xyz;
    float3 direction = mul(CameraInverseProjection, float4(uv, 0, 1)).xyz;
    direction = mul(CameraToWorld, float4(direction, 0)).xyz;
    direction = normalize(direction);

    return CreateRay(origin, direction);
}

HitData CreateHitData(float distance, float3 normal, float3 color)
{
    HitData hit;
    hit.distance = distance;
    hit.normal = normal;
    hit.color = color;

    return hit;
}

Step CreateStep(float distance, float3 color)
{
    Step step;
    step.distance = distance;
    step.color = color;

    return step;
}

Shape CreateShape(float3 position, float radius, float3 color)
{
    Shape shape;
    shape.position = position;
    shape.radius = radius;
    shape.color = color;

    return shape;
}


float SphereDistance(float3 position, Shape shape)
{
    return length(position - shape.position) - shape.radius;
}

float BoxDistance(float3 position, Shape shape)
{
    float3 o = abs(position - shape.position) - shape.size * 0.5f;
    float ud = length(max(o, 0));
    float n = min(max(o.x, max(o.y, o.z)), 0);

    return ud + n - shape.radius * 0.3f;
}

float OctahedronDistance(float3 position, Shape shape)
{
    float3 p = position - shape.position;
    p = abs(p);
    
    return (p.x + p.y + p.z - shape.radius) * 0.57735027f;
}

float PlaneDistance(float3 position, Shape shape)
{
    return position.y - shape.position.y;
}

float TorusDistance(float3 position, Shape shape)
{
    float3 p = position - shape.position;
    float2 q = float2(length(p.xz) - shape.size.x, p.y);

    return length(q) - shape.size.y;
}

float ShapeDistance(float3 position, Shape shape)
{
    if (shape.type == ShapeSphere)
        return SphereDistance(position, shape);
    else if (shape.type == ShapeBox)
        return BoxDistance(position, shape);
    else if (shape.type == ShapeOctahedron)
        return OctahedronDistance(position, shape);
    else if (shape.type == ShapePlane)
        return PlaneDistance(position, shape);
    else if (shape.type == ShapeTorus)
        return TorusDistance(position, shape);

    return 0;
}

Step StepFromShape(Shape shape, float3 position)
{
    Step step;
    step.distance = ShapeDistance(position, shape);
    step.color = shape.color;

    return step;
}

void Blend(inout Step current, float distance, float3 color, float k)
{
    float h = saturate(0.5f + 0.5f * (distance - current.distance) / k);

    current.distance = lerp(distance, current.distance, h) - k * h * (1.0 - h);
    current.color = lerp(color, current.color, h);
}

void Combine(inout Step current, Step childStep, Shape shape)
{
    float distance = childStep.distance;
    float3 color = childStep.color;

    if (shape.operation == OperationNone)
    {
        current.distance = min(current.distance, distance);
        if (current.distance == distance)
            current.color = color;
    }
    else if (shape.operation == OperationCut)
    {
        current.distance = max(current.distance, -distance);
        if (current.distance == -distance)
            current.color = color;
    }
    else if (shape.operation == OperationMask)
    {
        current.distance = max(current.distance, distance);
        if (current.distance == distance)
            current.color = color;
    }
    else if (shape.operation == OperationBlend)
        Blend(current, distance, color, shape.blendStrength);
}

Step GetStep(float3 position)
{
    uint count, stride;
    Shapes.GetDimensions(count, stride);

    Step result = CreateStep(MaxDistance, 0);

    for (uint i = 0; i < shapeCount; i++)
    {
        Shape shape = Shapes[i];
        Step shapeStep = StepFromShape(shape, position);

        for (int j = 1; j <= shape.childCount; j++)
        {
            Shape child = Shapes[i + j];
            Step childStep = StepFromShape(child, position);

            Combine(shapeStep, childStep, child);
        }
        i += shape.childCount;

        Combine(result, shapeStep, shape);
    }

    return result;
}

float GetDistance(float3 position)
{
    return GetStep(position).distance;
}

float3 GetNormal(float3 position)
{
    float distance = GetDistance(position);
    float2 e = float2(0.001f, 0);

    float3 normal = distance - float3(
        GetDistance(position - e.xyy),
        GetDistance(position - e.yxy),
        GetDistance(position - e.yyx));

    return normalize(normal);
}

HitData RayMarch(Ray ray)
{
    float distance = 0;
    float3 normal = 0;
    float3 color = 0;

    for (uint i = 0; i < MaxSteps; i++)
    {
        Step step = GetStep(ray.origin);
        ray.origin += step.distance * ray.direction;
        distance += step.distance;

        if (distance >= MaxDistance)
            break;

        if (step.distance <= MinDistance)
        {
            normal = GetNormal(ray.origin);
            color = step.color;
            break;
        }
    }

    return CreateHitData(distance, normal, color);
}


[numthreads(32,32,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    float width, height;
    Result.GetDimensions(width, height);

    uint stride;
    Shapes.GetDimensions(shapeCount, stride);

    float2 uv = id.xy / float2(width, height) * 2 - 1;
    Ray ray = CreateCameraRay(uv);

    HitData hit = RayMarch(ray);

    float3 lightDirection = float3(-1, -1, 1);
    float3 lightColor = float3(1.0f, 0.956f, 0.8392f);
    float lightIntensity = 0.65f;

    float light = dot(hit.normal, -lightDirection) + 1;
    float3 color = hit.color * lerp(0.25f, 1, light) * lightColor * lightIntensity;
    Result[id.xy] = float4(color, 0);
}
