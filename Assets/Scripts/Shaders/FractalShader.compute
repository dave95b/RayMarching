#pragma kernel CSMain

struct Ray
{
    float3 origin, direction;
};

struct FractalData
{
    float distance;
    uint iterations;
};

struct HitData
{
    float distance;
    float3 normal;
    uint rayIterations, fractalIterations;
};

static const uint MaxSteps = 48;
static const uint FractalIterations = 16;
static const float MinDistance = 0.01f;
static const float infinity = 3.402823466e+38;

RWTexture2D<float4> Result;

float4x4 CameraToWorld, CameraInverseProjection;

float Power, Darkness, BlackAndWhite, LightIntensity;
float3 ColorMix, LightColor, LightDirection;

Ray CreateRay(float3 origin, float3 direction)
{
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;

    return ray;
}

Ray CreateCameraRay(float2 uv)
{
    float3 origin = mul(CameraToWorld, float4(0, 0, 0, 1)).xyz;
    float3 direction = mul(CameraInverseProjection, float4(uv, 0, 1)).xyz;
    direction = mul(CameraToWorld, float4(direction, 0)).xyz;
    direction = normalize(direction);

    return CreateRay(origin, direction);
}

FractalData CreateFractalData(float distance, uint iterations)
{
    FractalData data;
    data.distance = distance;
    data.iterations = iterations;

    return data;
}

HitData CreateHitData(float distance, float3 normal, uint rayIterations, uint fractalIterations)
{
    HitData hit;
    hit.distance = distance;
    hit.normal = normal;
    hit.rayIterations = rayIterations;
    hit.fractalIterations = fractalIterations;

    return hit;
}


FractalData GetFractalData(float3 position)
{
    float3 z = position;
    float dr = 1;
    float r = 0;
    uint i = 0;

    for (i = 0; i < FractalIterations; i++)
    {
        r = length(z);
        if (r > 3.7) break;

        float theta = acos(z.z / r);
        float phi = atan2(z.y, z.x);
        dr = pow(r, Power - 1.0f) * Power * dr + 1.0f;

        float zr = pow(r, Power);
        theta *= Power;
        phi *= Power;

        z = zr * float3(sin(theta) * cos(phi), sin(phi) * sin(theta), cos(theta));
        z += position;
    }

    float distance = 0.5f * log(r) * r / dr;

    return CreateFractalData(distance, i);
}

float GetDistance(float3 position)
{
    return GetFractalData(position).distance;
}

float3 GetNormal(float3 position)
{
    float distance = GetDistance(position);
    float2 e = float2(0.015f, 0);

    float3 normal = distance - float3(
        GetDistance(position - e.xyy),
        GetDistance(position - e.yxy),
        GetDistance(position - e.yyx));

    return normalize(normal);
}

HitData RayMarch(Ray ray)
{
    float distance = 0;
    uint fractalIterations = 0;
    uint i;

    for (i = 0; i < MaxSteps; i++)
    {
        FractalData data = GetFractalData(ray.origin);
        ray.origin += data.distance * ray.direction;
        distance += data.distance;
        fractalIterations = data.iterations;

        if (data.distance <= MinDistance)
            break;
    }

    float3 normal = GetNormal(ray.origin - ray.direction * 0.01f);

    return CreateHitData(distance, normal, i, fractalIterations);
}

float3 GetLightColor(float3 normal)
{
    float light = dot(normal, -LightDirection) + 1;
    return lerp(0.25f, 1, light) * LightColor * LightIntensity;
}

[numthreads(32,32,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    float width, height;
    Result.GetDimensions(width, height);

    float2 uv = id.xy / float2(width, height) * 2 - 1;
    Ray ray = CreateCameraRay(uv);

    HitData hit = RayMarch(ray);

    float3 lightColor = GetLightColor(hit.normal);
    float3 distanceColor = float(hit.fractalIterations) / float(FractalIterations);
    float3 color = saturate(distanceColor * ColorMix + lightColor);

    float rim = hit.rayIterations / Darkness;
    color = lerp(color, 1, BlackAndWhite) * rim;

    Result[id.xy] = float4(color, 0);
}
